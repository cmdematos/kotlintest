/*
	This should be applied to the root project.  It applies config and repository settings to each project in the build.

	If there are cross-project compiler configuration standards, this is also a good place to define them.

	It also defines a wrapper task that uses the central gradle version and location configuration.
 */

allprojects {

	// Make scripts easier to reference by defining extension properties for them.
	def commonScript = { String name -> new File(rootProject.file('gradle/common'), name) }
	ext.configScript = commonScript('config.gradle');
	ext.kotlintestScript = commonScript('kotlintest.gradle')

	// Apply the config script to every project.
	apply from: configScript

	// Configure repository location.
	repositories {
		maven {
			url "${repositoryUrl}"
		}
	}

	// Example of plugin-specific configuration
	pluginManager.withPlugin('java') {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
		compileJava {
			options.compilerArgs += '-Xlint:all'
		}
	}
}

wrapper {
	gradleVersion = "${project.gradleVersion}"
	distributionUrl = "${project.distributionUrl}"
}
